record edge_t {
  src: ubit<64>;
  dst: ubit<64>
}

record node_t {
  edge_begin: ubit<64>;
  edge_end: ubit<64>
}

decor "#pragma SDS data copy(nodes[0:256])"
decor "#pragma SDS data zero_copy(level[0:256])"

decl nodes: node_t[256];
decl edges: edge_t[4096];
decl starting_node: ubit<64>;
decl level: bit<8>[256];
decl level_counts: ubit<64>[10];

let MAX_LEVEL = 127;

level[starting_node] := 0;
level_counts[0] := 1;
---

for (let horizon = 0..10) {
  decor "#pragma HLS loop_tripcount max=10 min=1"
  let cnt:ubit<64> = 0;
  for (let n = 0..256) {
    let tmp_l = level[n];
    ---
    if (tmp_l == horizon) {
      let e = nodes[n].edge_begin;
      ---
      let tmp_end = nodes[n].edge_end;
      ---
      while (e < tmp_end) {
        decor "#pragma HLS loop_tripcount avg=17"
        let tmp_dst = edges[e].dst;
        let tmp_level = level[tmp_dst];
        ---
        if (tmp_level == MAX_LEVEL) {
          level[tmp_dst] := horizon + 1;
          ---
          cnt := cnt + 1;
        }
        e := e + 1;
      }
    }
  }
  level_counts[horizon + 1] := cnt;
  if (cnt == 0) {
    horizon := 10;
  }
}
