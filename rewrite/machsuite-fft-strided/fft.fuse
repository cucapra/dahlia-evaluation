decor "#pragma SDS data zero_copy(real[0:1024], img[0:1024])"

decl real: double[1024];
decl img: double[1024];
decl real_twid: double[512];
decl img_twid: double[512];

let span:bit<32> = 1024 >> 1;
let log:bit<32> = 0;

while (span != 0) {
  let odd = span;
  while (odd < 1024) {
    decor "#pragma HLS loop_tripcount max=1024 min=512"
    odd := odd | span;
    let even = odd ^ span;
    let real_odd = real[odd];
    let img_odd = img[odd];
    ---
    let real_even = real[even];
    let img_even = img[even];
    let temp_r = real_even + real_odd;
    let temp_i = img_even + img_odd;
    ---
    real[odd] := real_even - real_odd;
    img[odd] := img_even - img_odd;
    ---
    real[even] := temp_r;
    img[even] := temp_i;
    let rootindex = (even << log) & (1024 - 1);
    ---
    if (rootindex != 0) {
      let temp = real_twid[rootindex] * real[odd] - img_twid[rootindex] * img[odd];
      img_odd := img[odd];
      ---
      img[odd] := real_twid[rootindex] * img_odd + img_twid[rootindex] * real[odd];
      ---
      real[odd] := temp;
    }
    odd := odd + 1;
  }
  span := span >> 1;
  log := log + 1;
}
